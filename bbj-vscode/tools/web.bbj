rem This BBj program is called from the Commands.js JavaScript program
rem These are the command line parameters:
rem   1) client [BUI|DWC]
rem   2) app name
rem   3) program file
rem   4) working directory
rem   5) username
rem   6) password
rem   7) session-specific classpath
rem   8) auth token (optional, if present uses token-based auth)

? 'HIDE'

isDWC!      = iff(ARGV(1) = "DWC", 1, 0)
name!       = ARGV(2)
programme!  = ARGV(3)
wd!         = ARGV(4)
username!   = ARGV(5,err=*next)
password!   = ARGV(6,err=*next)
sscp!       = ARGV(7,err=*next)
token!      = ARGV(8,err=*next)

rem Token-based authentication if token is provided
if (token! <> null() and token! > "") then
    admin! = BBjAdminFactory.getBBjAdmin(token!)
else
    rem Legacy username/password authentication (backward compatible)
    if (username! = null()) username! = "admin"
    if (password! = null()) password! = "admin123"
    admin! = BBjAdminFactory.getBBjAdmin(username!, password!)
fi
configuration! = admin!.getRemoteConfiguration()

rem clean up name!
name! = name!.replaceAll("-","")

rem get the app if it's already registered
app! = null()
apps! = configuration!.getApplications()
it! = apps!.iterator()
while(it!.hasNext())
    currentApp! = it!.next()
    appName! = currentApp!.getString(currentApp!.NAME)
    if (appName! = name!) then
        app! = currentApp!
        break
    fi
wend

rem create the app if it wasn't in the list of registered apps
if app! = null() then
    app! = configuration!.createApplication()
fi

rem set the properties for the application
app!.setString(app!.NAME, name!)
app!.setString(app!.PROGRAM, programme!)
app!.setString(app!.WORKING_DIRECTORY , wd!)
app!.setString(app!.CONFIG_FILE, BBjAPI().getConfig().getConfigFileName())

if sscp! <> null() and sscp! > "" and sscp! <> "--" then
    app!.setString(app!.CLASSPATH, sscp!)
fi

if (isDWC!) then app!.setBoolean(app!.DWC_ENABLED, 1)
app!.commit()

rem open the app in the browser
url! = iff(isDWC!, app!.getDwcUrl(0), app!.getBuiUrl(0))
BBjAPI().getThinClient().browse(url!)

release